plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.5'
    id 'io.spring.dependency-management' version '1.1.0'
}

group = 'uz.school'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.hibernate.validator:hibernate-validator'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('copyJarToDockerDir', Copy) {
    from "$buildDir/libs"
    include "**/*.jar"
    into "$buildDir/docker"
}

tasks.register('prepareDockerDir', Copy) {
    from "$projectDir/docker"
    include "**/*"
    filter { it.replaceAll('<%=name%>', project.name) }
    into "$buildDir/docker"
}

tasks.register('buildDockerImage', Exec) {
    workingDir "$buildDir"
    commandLine "docker", "build", "-t", "naughtydog/schoolwebapp:${project.version}", "--build-arg", "JAR_FILE=SchoolWebApp-0.0.1-SNAPSHOT.jar", "docker"
}

tasks.register('pushDockerImage', Exec) {
    commandLine "docker", "push", "naughtydog/schoolwebapp:${project.version}"
}

copyJarToDockerDir.dependsOn build
prepareDockerDir.dependsOn copyJarToDockerDir
buildDockerImage.dependsOn prepareDockerDir
pushDockerImage.dependsOn buildDockerImage